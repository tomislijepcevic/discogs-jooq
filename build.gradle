import com.github.spotbugs.SpotBugsTask

plugins {
    id 'java'
    id 'com.diffplug.gradle.spotless' version '3.24.0'
    id 'com.github.spotbugs' version '2.0.0'
    id 'idea'
    id 'nu.studer.jooq' version '3.0.3'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    jooqVersion = '3.11.11'
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

idea {
    module {
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
    }
}

jooq {
    version = jooqVersion
    edition = 'OSS'
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:5432/discogs'
            user = 'discogs'
            password = 'discogs'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'discogs'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                fluentSetters = true
            }
            target {
                packageName = 'tslic.discogs'
                directory = 'src/generated/jooq'
            }
        }
    }
}

// removes the task dependency from compileJava to generateSampleJooqSchemaSource
project.tasks.getByName('compileJava').dependsOn -= 'generateSampleJooqSchemaSource'

dependencies {
    jooqRuntime 'org.postgresql:postgresql:42.2.6'
    compile "org.jooq:jooq:$jooqVersion"
    compile 'javax.annotation:javax.annotation-api:1.3.2'
}

group = 'tslic.discogs'
version = '1.0'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
